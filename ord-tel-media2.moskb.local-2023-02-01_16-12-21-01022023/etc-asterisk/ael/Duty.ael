// Duty agent validation
context duty { // Duty agent validation call passes through this context
 h => {
  status=;
  if (${HANGUPCAUSE}>0 && (${HANGUPCAUSE}<16 || ${HANGUPCAUSE}>31)) // Some channel troubles?
  {
   Set(ODBC_DUTY_AGENT(${nrec})=);
  } else
   switch ("${DIALSTATUS}")
   {
    case "ANSWER": break; //ANSWER must be processed by duty_agent !!!
    case "BUSY": Set(ODBC_DUTY_AGENT(${nrec})=); break;
    case "NOANSWER": Set(ODBC_DUTY_AGENT(${nrec})=0,1); break;
    case "CANCEL": Set(ODBC_DUTY_AGENT(${nrec})=0,1); break;
    default: Set(ODBC_DUTY_AGENT(${nrec})=);
   }
//  Set(CDR(pbxserver)=${FILTER(0-9a-zA-Z\.\-,${SHELL(hostname)})});
  Set(CDR(pbxserver)=${PBXSERVER});
  Hangup();
 }

 _. => {
  if ("${EXTEN}"="failed")
  {
   failed=1;
   Hangup;
  }
  Set(CHANNEL(language)=${DEFAULT_LANG});
  nrec=${EXTEN};
  Set(HASH(agent)=${ODBC_DUTY_AGENT(${nrec})});
  Set(__BIND=${HASH(agent,BIND)});

  if ("${HASH(agent,Announce)}"!="") Dial(LOCAL/${HASH(agent,Exten)}-${HASH(agent,Agent)}@out/n,${HASH(agent,Timeout)},fg);
  Hangup;
 }
} // context duty

context duty_agent { // Agent validation
 h => {
  if (${valid}=1) {
   Set(ODBC_DUTY_AGENT(${nrec})=1);
  } else
  {
   Set(ODBC_DUTY_AGENT(${nrec})=0,1);
  }
 }

 _X => {
   nrec=${EXTEN};
   goto s|1;
  }

 _X. => {
   nrec=${EXTEN};
   goto s|1;
  }

 s => {
  Set(CHANNEL(language)=${DEFAULT_LANG});
  valid=0;
  Set(HASH(agent)=${ODBC_DUTY_AGENT(${nrec})});

  if ("${HASH(agent,Announce)}"!="")
  {
   Wait(1);

   retry:
   num=;
   Read(num,auto-informer/${HASH(agent,Announce)}&auto-informer/${HASH(agent,Announce)},1,,,10);
   if ("${num}"="") {
    if ("${READSTATUS}"="OK") { // "#" pressed (twice)
     valid=1;
     Playback(vm-thanks);
    }
   }
  }
  Hangup;
 }
} // context duty_agent
